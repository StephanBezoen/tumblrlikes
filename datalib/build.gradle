buildscript {

    repositories {
        mavenCentral()
        maven { url rootProject.ext.Repos.fabric }
    }

    dependencies {
        def classpaths = rootProject.ext.ClassPaths

        classpath classpaths.greenDAO
        classpath classpaths.fabric
    }
}

def config = rootProject.ext
def plugins = config.Plugins
def libs = config.Libraries

apply plugin: plugins.android_lib
apply plugin: plugins.greenDAO
apply plugin: plugins.kotlin
apply plugin: plugins.kotlin_kapt

apply from: "$rootDir/config/config.gradle", to: config

android {
    def versions = config.Versions

    compileSdkVersion versions.compileSdk

    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.compileSdk

        versionCode 1
        versionName "1.0"

        testInstrumentationRunner libs.test_runner

        buildConfigField "String", "CONSUMER_KEY", "\"" + config.consumer_key + "\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

afterEvaluate {
    if (project.plugins.hasPlugin('org.greenrobot.greendao')) {
        def taskGraph = project.getGradle().getTaskGraph()
        taskGraph.whenReady {
            Task greendaoPrepareTask = project.tasks.getByName("greendaoPrepare")
            Task greendaoTask = project.tasks.getByName("greendao")

            Task debugKotlin = project.tasks.getByName("kaptDebugKotlin")
            Task releaseKotlin = project.tasks.getByName("kaptReleaseKotlin")
            debugKotlin.doFirst {
                greendaoPrepareTask.execute()
                greendaoTask.execute()
            }
            releaseKotlin.doFirst {
                greendaoPrepareTask.execute()
                greendaoTask.execute()
            }
        }
    }
}

greendao {
    schemaVersion 8
    daoPackage 'nl.acidcats.tumblrlikes.db_impl_greendao'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation libs.kotlin

    // GreenDAO
    implementation libs.greenDAO

    // Rx
    implementation libs.rxAndroid
    implementation libs.rxKotlin

    // Retrofit
    implementation libs.retrofit
    implementation libs.retrofit_moshiConverter
    implementation libs.retrofit_rxAdapter

    // OkHttp
    implementation libs.okHttp
    implementation libs.okHttp_loggingInterceptor

    // Dagger
    implementation libs.dagger
    kapt libs.annotationProcessorDagger

    // Moshi
    implementation libs.moshi_kotlin
    kapt libs.moshi_codegen

    // Timber
    implementation libs.timber
}

repositories {
    mavenCentral()
}
