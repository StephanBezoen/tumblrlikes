buildscript {

    repositories {
        mavenCentral()
        maven { url rootProject.ext.Repos.fabric }
    }

    dependencies {
        def classpaths = rootProject.ext.ClassPaths

        classpath classpaths.hugo
        classpath classpaths.retrolambda
        classpath classpaths.greenDAO
        classpath classpaths.fabric
    }
}

def config = rootProject.ext
def plugins = config.Plugins
def libs = config.Libraries
def repos = config.Repos

apply plugin: plugins.android_app
apply plugin: 'kotlin-android'
apply plugin: plugins.hugo
apply plugin: plugins.retrolambda
apply plugin: plugins.greenDAO
apply plugin: plugins.fabric

apply from: "$rootDir/config/config.gradle", to: config

android {
    def versions = config.Versions
    def appConfig = config.AppConfig

    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.compileSdk

        applicationId appConfig.id
        versionCode appConfig.versionCode
        versionName appConfig.versionName

        testInstrumentationRunner libs.test_runner

        vectorDrawables.useSupportLibrary = true

        buildConfigField "String", "CONSUMER_KEY", "\"" + config.consumer_key + "\""
        if (config.has('blog')) {
            buildConfigField "String", "BLOG", "\"" + config.blog + "\""
        } else {
            buildConfigField "String", "BLOG", "\"\""
        }
    }

    signingConfigs {
        release_keystore {
            storeFile file(config.keystore_file)
            storePassword config.keystore_password
            keyAlias config.keystore_key
            keyPassword config.key_password
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release_keystore
        }
        debug {
            applicationIdSuffix appConfig.debugSuffix
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

greendao {
    schemaVersion 5
}

repositories {
    maven { url repos.fabric }
    mavenCentral()
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // testing
    androidTestCompile(libs.test_espresso, {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile libs.test_junit

    // Android Support
    compile libs.support_appCompat
    compile libs.support_design
    compile libs.support_constraintLayout

    // Rx
    compile libs.rxAndroid

    // Butterknife
    compile libs.butterknife
    annotationProcessor libs.annotationProcessorButterknife

    // Pixplicity Prefs
    compile libs.easyPrefs

    // GSon
    compile libs.gson

    // Retrofit
    compile libs.retrofit
    compile libs.retrofit_gsonConverter
    compile libs.retrofit_rxAdapter

    // Glide
    compile libs.glide

    // Dagger
    compile libs.dagger
    annotationProcessor libs.annotationProcessorDagger

    // OkHttp
    compile libs.okHttp
    compile libs.okHttp_loggingInterceptor

    // AutoValue
    provided libs.autoValue
    annotationProcessor libs.autoValue

    // AutoValue Gson
    provided libs.autoValue_gson
    annotationProcessor libs.autoValue_gson

    // GreenDAO
    compile libs.greenDAO

    // Stetho
    compile libs.stetho

    // SQL Cipher, to get rid of GreenDAO exceptions
    compile libs.sqlCipher

    // Fabric
    compile(libs.fabric) {
        transitive = true;
    }
    compile libs.firebase
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
}

apply plugin: 'com.google.gms.google-services'